apply plugin: 'com.android.application'

android {
    compileSdkVersion 27

    defaultConfig {
        def buildPropsFile = file('build.properties')
        def buildNumber

        if (buildPropsFile.canRead()) {
            Properties buildProps = new Properties()
            buildProps.load(new FileInputStream(buildPropsFile))
            buildNumber = buildProps['build.number'].toInteger()
        } else {
            throw new FileNotFoundException("Could not read build.properties!")
        }

        def appPropsFile = file('app.properties')
        def packageName = ""
        def domainLimit = "15000"
        def domainPrefix = "false"
        def sklKey = ""
        def backwardsKey = ""
        def disableApps = "false"
        def appComponent = "false"

        if (appPropsFile.exists()) {
            Properties appProps = new Properties()
            appProps.load(new FileInputStream(appPropsFile))
            if (appProps['package.name'] != null) {
                packageName = appProps['package.name']
            }
            if (appProps['domain.limit'] != null) {
                domainLimit = appProps['domain.limit']
            }
            if (appProps['skl.key'] != null) {
                sklKey = appProps['skl.key']
            }
            if (appProps['backwards.key'] != null) {
                backwardsKey = appProps['backwards.key']
            }
            if (appProps['domain.prefix'] != null) {
                domainPrefix = appProps['domain.prefix']
            }
            if (appProps['enable.disableApps'] != null) {
                disableApps = appProps['enable.disableApps']
            }
            if (appProps['enable.appComponent'] != null) {
                appComponent = appProps['enable.appComponent']
            }
        }

        applicationId packageName
        minSdkVersion 23
        targetSdkVersion 27
        versionCode 1
        versionName "3.1.1." + buildNumber

        buildConfigField("int", "DOMAIN_LIMIT", domainLimit)
        buildConfigField("boolean", "DOMAIN_PREFIX", domainPrefix)
        buildConfigField("String", "SKL_KEY", '"' + sklKey + '"')
        buildConfigField("String", "BACKWARDS_KEY", '"' + backwardsKey + '"')
        buildConfigField("boolean", "DISABLE_APPS", disableApps)
        buildConfigField("boolean", "APP_COMPONENT", appComponent)
    }

    buildTypes {
        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
    }
}

dependencies {
    compileOnly files('libs/knoxsdk.jar')
    runtimeOnly files('libs/supportlib.jar')
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:support-v4:27.1.1'
    implementation 'com.android.support:recyclerview-v7:27.1.1'
    implementation 'com.android.support:percent:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support:preference-v14:27.1.1'
    implementation 'com.android.support:support-fragment:27.1.1'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.12'
    implementation 'android.arch.lifecycle:extensions:1.1.1'
    annotationProcessor "android.arch.lifecycle:compiler:1.1.1"
    implementation 'android.arch.persistence.room:runtime:1.1.1'
    annotationProcessor "android.arch.persistence.room:compiler:1.1.1"
    implementation 'android.arch.persistence.room:rxjava2:1.1.1'
    implementation 'com.google.dagger:dagger:2.15'
    annotationProcessor "com.google.dagger:dagger-compiler:2.15"
    compileOnly 'javax.annotation:jsr250-api:1.0'
    implementation 'com.google.guava:guava:24.1-android'
    implementation 'com.getbase:floatingactionbutton:1.10.1'
}